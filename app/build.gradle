plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    namespace "com.tcn.bicicas"

    compileSdk 34

    defaultConfig {
        applicationId "com.tcn.bicicas"
        minSdk 21
        targetSdk 34
        versionCode 2010
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.4"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation "androidx.activity:activity-compose:1.8.1"

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-process:2.7.0-rc01'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.7.0-rc01'

    // Compose
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.ui:ui"
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation 'androidx.navigation:navigation-compose:2.7.5'

    // Dependency injection
    implementation 'io.insert-koin:koin-android:3.5.0'
    implementation 'io.insert-koin:koin-androidx-compose:3.5.0'
    testImplementation 'io.insert-koin:koin-test:3.5.0'
    testImplementation 'io.insert-koin:koin-test-junit4:3.5.0'

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'org.json:json:20231013'

    // Maps
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.maps.android:maps-ktx:5.0.0'

    // Encryption
    implementation 'org.jasypt:jasypt:1.9.3'

    // OS Licenses
    implementation 'com.mikepenz:aboutlibraries-core:10.9.2'

    // If PIN_GENERATOR_LIB_PATH is not found then a dummy library is used for pin generation
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def pinGeneratorLibPath = properties.getProperty('PIN_GENERATOR_LIB_PATH') ?: 'libs/bicicas-pin-generator-dummy.jar'
    implementation files(pinGeneratorLibPath)

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'app.cash.turbine:turbine:1.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.5.4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}