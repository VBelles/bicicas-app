plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    namespace "com.tcn.bicicas"

    compileSdk 33

    defaultConfig {
        applicationId "com.tcn.bicicas"
        minSdk 21
        targetSdk 33
        versionCode 2010
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-splashscreen:1.0.0'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.5.1'

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2022.11.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.ui:ui"
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation 'androidx.navigation:navigation-compose:2.5.3'

    // Compose extensions
    def accompanist_version = '0.27.1'
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // Dependency injection
    def koin_version = "3.3.0"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.9'
    testImplementation 'org.json:json:20220924'

    // Maps
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.maps.android:maps-ktx:3.4.0'

    // Encryption
    implementation 'org.jasypt:jasypt:1.9.3'

    // OS Licenses
    implementation 'com.mikepenz:aboutlibraries-core:10.5.2'
    implementation 'com.mikepenz:aboutlibraries-compose:10.5.2'


    // If PIN_GENERATOR_LIB_PATH is not found then a dummy library is used for pin generation
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def pinGeneratorLibPath = properties.getProperty('PIN_GENERATOR_LIB_PATH') ?: 'libs/bicicas-pin-generator.jar'
    implementation files(pinGeneratorLibPath)

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'app.cash.turbine:turbine:0.12.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}